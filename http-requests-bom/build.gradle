/*
 * Copyright 2016-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "${rootDir}/gradle/bintray.gradle"

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        library(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            pom {
                name = project.name
                description = projectDescription
                url = githubHttpsUrl

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'budjb'
                        name = 'Bud Byrd'
                        email = 'bud.byrd@gmail.com'
                    }
                }
                scm {
                    url = githubHttpsUrl
                    connection = "scm:git:" + githubHttpsUrl + ".git"
                    developerConnection = "scm:git:" + githubHttpsUrl + ".git"
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = githubHttpsUrl + "/issues"
                }

                withXml {
                    asNode().children().last() + {
                        def builder = delegate

                        builder.dependencyManagement {
                            builder.dependencies {
                                for (sub in rootProject.subprojects) {
                                    if (!sub.plugins.hasPlugin('java') || !sub.plugins.hasPlugin('maven-publish')) {
                                        continue
                                    }

                                    builder.dependency {
                                        builder.groupId sub.group
                                        builder.artifactId sub.name
                                        builder.version sub.version
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
